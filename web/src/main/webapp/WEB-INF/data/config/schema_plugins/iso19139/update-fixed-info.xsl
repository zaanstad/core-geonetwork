<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="2.0"
	xmlns:gml="http://www.opengis.net/gml" xmlns:srv="http://www.isotc211.org/2005/srv"
	xmlns:gmx="http://www.isotc211.org/2005/gmx" xmlns:gco="http://www.isotc211.org/2005/gco"
	xmlns:util="java:java.util.UUID"
	xmlns:gmd="http://www.isotc211.org/2005/gmd" xmlns:xlink="http://www.w3.org/1999/xlink" exclude-result-prefixes="#all">

	<xsl:include href="../iso19139/convert/functions.xsl"/>

	<!-- ================================================================= -->

	<xsl:template match="/root">
		<xsl:apply-templates select="gmd:MD_Metadata"/>
	</xsl:template>

	<!-- ================================================================= -->

	<xsl:template match="gmd:MD_Metadata">
		<xsl:copy>
			<xsl:apply-templates select="@*"/>
			
			<gmd:fileIdentifier>
				<gco:CharacterString>
					<xsl:value-of select="/root/env/uuid"/>
				</gco:CharacterString>
			</gmd:fileIdentifier>
			
			<xsl:apply-templates select="gmd:language"/>
			<xsl:apply-templates select="gmd:characterSet"/>
			
			<xsl:choose>
				<xsl:when test="/root/env/parentUuid!=''">
					<gmd:parentIdentifier>
						<gco:CharacterString>
							<xsl:value-of select="/root/env/parentUuid"/>
						</gco:CharacterString>
					</gmd:parentIdentifier>
				</xsl:when>
				<xsl:when test="gmd:parentIdentifier">
					<xsl:copy-of select="gmd:parentIdentifier"/>
				</xsl:when>
			</xsl:choose>
			<xsl:apply-templates select="node()[not(self::gmd:language) and not(self::gmd:characterSet)]"/>
		</xsl:copy>
	</xsl:template>


	<!-- ================================================================= -->
	<!-- Do not process MD_Metadata header generated by previous template  -->

	<xsl:template match="gmd:MD_Metadata/gmd:fileIdentifier|gmd:MD_Metadata/gmd:parentIdentifier" priority="10"/>

	<!-- ================================================================= -->

	<xsl:template match="gmd:dateStamp">
    <xsl:choose>
        <xsl:when test="/root/env/changeDate">
            <xsl:copy>
                    <gco:DateTime>
                        <xsl:value-of select="/root/env/changeDate"/>
                    </gco:DateTime>
            </xsl:copy>
        </xsl:when>
        <xsl:otherwise>
            <xsl:copy-of select="."/>
        </xsl:otherwise>
    </xsl:choose>
	</xsl:template>

	<!-- ================================================================= -->
	
	<!-- Only set metadataStandardName and metadataStandardVersion
	if not set. -->
	<xsl:template match="gmd:metadataStandardName[@gco:nilReason='missing' or gco:CharacterString='']" priority="10">
		<xsl:copy>
			<gco:CharacterString>ISO 19115:2003/19139</gco:CharacterString>
		</xsl:copy>
	</xsl:template>
	
	<xsl:template match="gmd:metadataStandardVersion[@gco:nilReason='missing' or gco:CharacterString='']" priority="10">
		<xsl:copy>
			<gco:CharacterString>1.0</gco:CharacterString>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->
	
	<xsl:template match="@gml:id">
		<xsl:choose>
			<xsl:when test="normalize-space(.)=''">
				<xsl:attribute name="gml:id">
					<xsl:value-of select="generate-id(.)"/>
				</xsl:attribute>
			</xsl:when>
			<xsl:otherwise>
				<xsl:copy-of select="."/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<!-- ==================================================================== -->
	<!-- Fix srsName attribute generate CRS:84 (EPSG:4326 with long/lat 
	     ordering) by default -->

	<xsl:template match="@srsName">
		<xsl:choose>
			<xsl:when test="normalize-space(.)=''">
				<xsl:attribute name="srsName">
					<xsl:text>CRS:84</xsl:text>
				</xsl:attribute>
			</xsl:when>
			<xsl:otherwise>
				<xsl:copy-of select="."/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
  
  <!-- Add required gml attributes if missing -->
  <xsl:template match="gml:Polygon[not(@gml:id) and not(@srsName)]">
    <xsl:copy>
      <xsl:attribute name="gml:id">
        <xsl:value-of select="generate-id(.)"/>
      </xsl:attribute>
      <xsl:attribute name="srsName">
        <xsl:text>urn:x-ogc:def:crs:EPSG:6.6:4326</xsl:text>
      </xsl:attribute>
      <xsl:copy-of select="@*"/>
      <xsl:copy-of select="*"/>
    </xsl:copy>
  </xsl:template>
  
	<!-- ================================================================= -->
	
	<xsl:template match="*[gco:CharacterString]">
		<xsl:copy>
			<xsl:apply-templates select="@*[not(name()='gco:nilReason')]"/>
			<xsl:choose>
				<xsl:when test="normalize-space(gco:CharacterString)=''">
					<xsl:attribute name="gco:nilReason">
						<xsl:choose>
							<xsl:when test="@gco:nilReason"><xsl:value-of select="@gco:nilReason"/></xsl:when>
							<xsl:otherwise>missing</xsl:otherwise>
						</xsl:choose>
					</xsl:attribute>
				</xsl:when>
				<xsl:when test="@gco:nilReason!='missing' and normalize-space(gco:CharacterString)!=''">
					<xsl:copy-of select="@gco:nilReason"/>
				</xsl:when>
			</xsl:choose>
			<xsl:apply-templates select="node()"/>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->
	<!-- codelists: set @codeList path -->
	<!-- ================================================================= -->
	<xsl:template match="gmd:LanguageCode[@codeListValue]" priority="10">
		<gmd:LanguageCode codeList="http://www.loc.gov/standards/iso639-2/">
			<xsl:apply-templates select="@*[name(.)!='codeList']"/>
		</gmd:LanguageCode>
	</xsl:template>
	
	
	<xsl:template match="gmd:*[@codeListValue]">
		<xsl:copy>
			<xsl:apply-templates select="@*"/>
			<xsl:attribute name="codeList">
			  <xsl:value-of select="concat('http://standards.iso.org/ittf/PubliclyAvailableStandards/ISO_19139_Schemas/resources/codelist/ML_gmxCodelists.xml#',local-name(.))"/>
			</xsl:attribute>
		</xsl:copy>
	</xsl:template>

	<!-- can't find the location of the 19119 codelists - so we make one up -->

	<xsl:template match="srv:*[@codeListValue]">
		<xsl:copy>
			<xsl:apply-templates select="@*"/>
			<xsl:attribute name="codeList">
				<xsl:value-of select="concat('http://www.isotc211.org/2005/iso19119/resources/Codelist/gmxCodelists.xml#',local-name(.))"/>
			</xsl:attribute>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->
	<!-- online resources: download -->
	<!-- ================================================================= -->
	<xsl:template match="gmd:CI_OnlineResource[matches(gmd:protocol/gco:CharacterString,'^WWW:DOWNLOAD-.*-http--download.*') and gmd:name]">
		<xsl:variable name="fname" select="gmd:name/gco:CharacterString|gmd:name/gmx:MimeFileType"/>
		<xsl:variable name="mimeType">
			<xsl:call-template name="getMimeTypeFile">
				<xsl:with-param name="datadir" select="/root/env/datadir"/>
				<xsl:with-param name="fname" select="$fname"/>
			</xsl:call-template>
		</xsl:variable>

		<xsl:copy>
			<xsl:copy-of select="@*"/>
			<gmd:linkage>
				<gmd:URL>
					<xsl:choose>
						<xsl:when test="/root/env/config/downloadservice/simple='true'">
							<xsl:value-of select="concat(/root/env/siteURL,'/resources.get?uuid=',/root/env/uuid,'&amp;fname=',$fname,'&amp;access=private')"/>
						</xsl:when>
						<xsl:when test="/root/env/config/downloadservice/withdisclaimer='true'">
							<xsl:value-of select="concat(/root/env/siteURL,'/file.disclaimer?uuid=',/root/env/uuid,'&amp;fname=',$fname,'&amp;access=private')"/>
						</xsl:when>
						<xsl:otherwise> <!-- /root/env/config/downloadservice/leave='true' -->
							<xsl:value-of select="gmd:linkage/gmd:URL"/>
						</xsl:otherwise>
					</xsl:choose>
				</gmd:URL>
			</gmd:linkage>
			<xsl:copy-of select="gmd:protocol"/>
			<xsl:copy-of select="gmd:applicationProfile"/>
			<gmd:name>
				<gmx:MimeFileType type="{$mimeType}">
					<xsl:value-of select="$fname"/>
				</gmx:MimeFileType>
			</gmd:name>
			<xsl:copy-of select="gmd:description"/>
			<xsl:copy-of select="gmd:function"/>
		</xsl:copy>
	</xsl:template>


	<!-- Add resource identifier -->
	<xsl:template match="gmd:MD_DataIdentification/gmd:citation">
		<xsl:choose>
			<xsl:when test="count(gmd:CI_Citation/gmd:identifier[gmd:MD_Identifier/gmd:code/gco:CharacterString != '']) > 0">
				<xsl:copy>
					<xsl:copy-of select="@*" />
					<xsl:apply-templates select="node()" />
				</xsl:copy>
			</xsl:when>
			<xsl:otherwise>
				<xsl:copy>
					<xsl:copy-of select="@*" />
					<gmd:CI_Citation>
						<xsl:apply-templates select="gmd:CI_Citation/gmd:title" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:alternateTitle" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:date" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:edition" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:editionDate" />

						<xsl:apply-templates select="gmd:CI_Citation/gmd:identifier[not(gmd:MD_Identifier)]" />

						<xsl:choose>
							<!-- There's a gmd:code, keep it -->
							<xsl:when test="string(gmd:identifier/gmd:MD_Identifier/gmd:code/gco:CharacterString)">
								<xsl:apply-templates select="gmd:identifier" />
							</xsl:when>
							<!-- No gmd:identifier -->
							<xsl:when test="not(gmd:CI_Citation/gmd:identifier)">
								<gmd:identifier>
									<gmd:MD_Identifier>
										<xsl:call-template name="AddCode"  />
									</gmd:MD_Identifier>
								</gmd:identifier>
							</xsl:when>
							<!-- No gmd:identifier with gmd:MD_Identifier -->
							<xsl:when test="count(gmd:CI_Citation/gmd:identifier[gmd:MD_Identifier]) = 0">
								<gmd:identifier>
									<gmd:MD_Identifier>
										<xsl:call-template name="AddCode"  />
									</gmd:MD_Identifier>
								</gmd:identifier>
							</xsl:when>

							<xsl:otherwise>
								<xsl:apply-templates select="gmd:CI_Citation/gmd:identifier[gmd:MD_Identifier][position()!=1]" />

								<xsl:for-each select="gmd:CI_Citation/gmd:identifier[gmd:MD_Identifier][1]">
									<xsl:copy>
										<xsl:copy-of select="@*" />
										<xsl:for-each select="gmd:MD_Identifier">
											<xsl:copy>
												<xsl:copy-of select="@*" />
												<xsl:apply-templates select="gmd:authority" />
												<xsl:call-template name="AddCode"  />
											</xsl:copy>
										</xsl:for-each>
									</xsl:copy>
								</xsl:for-each>
							</xsl:otherwise>
						</xsl:choose>

						<xsl:apply-templates select="gmd:CI_Citation/gmd:citedResponsibleParty" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:presentationForm" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:series" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:otherCitationDetails" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:collectiveTitle" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:ISBN" />
						<xsl:apply-templates select="gmd:CI_Citation/gmd:ISSN" />
					</gmd:CI_Citation>
				</xsl:copy>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template name="AddCode">
		<gmd:code>
			<gco:CharacterString><xsl:value-of select="util:toString(util:randomUUID())" /></gco:CharacterString>
		</gmd:code>
	</xsl:template>

	<!-- ================================================================= -->
	<!-- online resources: link-to-downloadable data etc -->
	<!-- ================================================================= -->

	<xsl:template match="gmd:CI_OnlineResource[starts-with(gmd:protocol/gco:CharacterString,'WWW:LINK-') and contains(gmd:protocol/gco:CharacterString,'http--download')]">
		<xsl:variable name="mimeType">
			<xsl:call-template name="getMimeTypeUrl">
				<xsl:with-param name="linkage" select="gmd:linkage/gmd:URL"/>
			</xsl:call-template>
		</xsl:variable>

		<xsl:copy>
			<xsl:copy-of select="@*"/>
			<xsl:copy-of select="gmd:linkage"/>
			<xsl:copy-of select="gmd:protocol"/>
			<xsl:copy-of select="gmd:applicationProfile"/>
			<gmd:name>
				<gmx:MimeFileType type="{$mimeType}"/>
			</gmd:name>
			<xsl:copy-of select="gmd:description"/>
			<xsl:copy-of select="gmd:function"/>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->

  <xsl:template match="gmx:FileName[name(..)!='gmd:contactInstructions']">
    <xsl:copy>
			<xsl:attribute name="src">
				<xsl:choose>
					<xsl:when test="/root/env/config/downloadservice/simple='true'">
						<xsl:value-of select="concat(/root/env/siteURL,'/resources.get?uuid=',/root/env/uuid,'&amp;fname=',.,'&amp;access=private')"/>
					</xsl:when>
					<xsl:when test="/root/env/config/downloadservice/withdisclaimer='true'">
						<xsl:value-of select="concat(/root/env/siteURL,'/file.disclaimer?uuid=',/root/env/uuid,'&amp;fname=',.,'&amp;access=private')"/>
					</xsl:when>
					<xsl:otherwise> <!-- /root/env/config/downloadservice/leave='true' -->
						<xsl:value-of select="@src"/>
					</xsl:otherwise>
				</xsl:choose>
			</xsl:attribute>
			<xsl:value-of select="."/>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->

	<!-- Do not allow to expand operatesOn sub-elements 
		and constrain users to use uuidref attribute to link
		service metadata to datasets. This will avoid to have
		error on XSD validation. -->

	<xsl:template match="srv:operatesOn|gmd:featureCatalogueCitation">
        <xsl:copy>
        <xsl:copy-of select="@uuidref"/>
        <xsl:if test="@uuidref">
            <xsl:choose>
                <xsl:when test="not(string(@xlink:href)) or starts-with(@xlink:href, /root/env/siteURL)">
                    <xsl:attribute name="xlink:href">
                        <xsl:value-of select="concat(/root/env/siteURL,'/csw?service=CSW&amp;request=GetRecordById&amp;version=2.0.2&amp;outputSchema=http://www.isotc211.org/2005/gmd&amp;elementSetName=full&amp;id=',@uuidref)"/>
                    </xsl:attribute>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:copy-of select="@xlink:href"/>
                </xsl:otherwise>
            </xsl:choose>

        </xsl:if>
        </xsl:copy>

	</xsl:template>

	<!-- ================================================================= -->
	<!-- Set local identifier to the first 3 letters of iso code. Locale ids
		are used for multilingual charcterString using #iso2code for referencing.
	-->
	<xsl:template match="gmd:PT_Locale">
		<xsl:element name="gmd:{local-name()}">
			<xsl:variable name="id" select="upper-case(
				substring(gmd:languageCode/gmd:LanguageCode/@codeListValue, 1, 3))"/>

			<xsl:apply-templates select="@*"/>
			<xsl:if test="normalize-space(@id)='' or normalize-space(@id)!=$id">
				<xsl:attribute name="id">
					<xsl:value-of select="$id"/>
				</xsl:attribute>
			</xsl:if>
			<xsl:apply-templates select="node()"/>
		</xsl:element>
	</xsl:template>

	<!-- Apply same changes as above to the gmd:LocalisedCharacterString -->
	<xsl:variable name="language" select="//gmd:PT_Locale" /> <!-- Need list of all locale -->
	<xsl:template  match="gmd:LocalisedCharacterString">
		<xsl:element name="gmd:{local-name()}">
			<xsl:variable name="currentLocale" select="upper-case(replace(normalize-space(@locale), '^#', ''))"/>
			<xsl:variable name="ptLocale" select="$language[upper-case(replace(normalize-space(@id), '^#', ''))=string($currentLocale)]"/>
			<xsl:variable name="id" select="upper-case(substring($ptLocale/gmd:languageCode/gmd:LanguageCode/@codeListValue, 1, 3))"/>
			<xsl:apply-templates select="@*"/>
			<xsl:if test="$id != '' and ($currentLocale='' or @locale!=concat('#', $id)) ">
				<xsl:attribute name="locale">
					<xsl:value-of select="concat('#',$id)"/>
				</xsl:attribute>
			</xsl:if>
			<xsl:apply-templates select="node()"/>
		</xsl:element>
	</xsl:template>

	<!-- ================================================================= -->
	<!-- Adjust the namespace declaration - In some cases name() is used to get the 
		element. The assumption is that the name is in the format of  <ns:element> 
		however in some cases it is in the format of <element xmlns=""> so the 
		following will convert them back to the expected value. This also corrects the issue 
		where the <element xmlns=""> loose the xmlns="" due to the exclude-result-prefixes="#all" -->
	<!-- Note: Only included prefix gml, gmd and gco for now. -->
	<!-- TODO: Figure out how to get the namespace prefix via a function so that we don't need to hard code them -->
	<!-- ================================================================= -->

	<xsl:template name="correct_ns_prefix">
		<xsl:param name="element" />
		<xsl:param name="prefix" />
		<xsl:choose>
			<xsl:when test="local-name($element)=name($element) and $prefix != '' ">
				<xsl:element name="{$prefix}:{local-name($element)}">
					<xsl:apply-templates select="@*|node()"/>
				</xsl:element>
			</xsl:when>
			<xsl:otherwise>
				<xsl:copy>
					<xsl:apply-templates select="@*|node()"/>
				</xsl:copy>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template match="gmd:*">
		<xsl:call-template name="correct_ns_prefix">
			<xsl:with-param name="element" select="."/>
			<xsl:with-param name="prefix" select="'gmd'"/>
		</xsl:call-template>
	</xsl:template>

	<xsl:template match="gco:*">
		<xsl:call-template name="correct_ns_prefix">
			<xsl:with-param name="element" select="."/>
			<xsl:with-param name="prefix" select="'gco'"/>
		</xsl:call-template>
	</xsl:template>

	<xsl:template match="gml:*">
		<xsl:call-template name="correct_ns_prefix">
			<xsl:with-param name="element" select="."/>
			<xsl:with-param name="prefix" select="'gml'"/>
		</xsl:call-template>
	</xsl:template>


	<xsl:template match="gmd:transferOptions">
		<xsl:copy>
			<xsl:copy-of select="@*" />
			<xsl:for-each select="gmd:MD_DigitalTransferOptions">
				<xsl:copy>
					<xsl:copy-of select="@*" />

					<!-- Copy all online resources except the ones with application/vnd.ogc.wms_xml protocol -->
					<xsl:apply-templates select="gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) != 'application/vnd.ogc.wms_xml']" />

					<!-- For each WMS with protocol application/vnd.ogc.wms_xml, fix the protocol to OGC:WMS -->
					<xsl:for-each select="gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'application/vnd.ogc.wms_xml']">
						<xsl:copy>
							<xsl:copy-of select="@*" />

							<xsl:for-each select="gmd:CI_OnlineResource">
								<xsl:copy>
									<xsl:copy-of select="@*" />

									<xsl:apply-templates select="gmd:linkage" />

									<gmd:protocol>
										<gco:CharacterString>OGC:WMS</gco:CharacterString>
									</gmd:protocol>

									<xsl:apply-templates select="gmd:applicationProfile" />
									<xsl:apply-templates select="gmd:name" />
									<xsl:apply-templates select="gmd:description" />
									<xsl:apply-templates select="gmd:function" />
								</xsl:copy>
							</xsl:for-each>
						</xsl:copy>
					</xsl:for-each>

					<!-- For each WMS, add WFS links if doesn't exists -->
					<xsl:for-each select="gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'application/vnd.ogc.wms_xml' or
                                                     normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'OGC:WMS']">
						<xsl:variable name="layerName" select="normalize-space(gmd:CI_OnlineResource/gmd:name/gco:CharacterString)"/>
						<xsl:variable name="wmsUrl" select="normalize-space(gmd:CI_OnlineResource/gmd:linkage/gmd:URL)"/>

						<xsl:variable name="qm"><xsl:if test="not(contains($wmsUrl,'?'))">?</xsl:if></xsl:variable>
						<xsl:variable name="wfsUrl" select="concat(replace(replace($wmsUrl, 'wms', 'wfs'), 'WMS', 'WFS'),$qm)"/>

						<xsl:variable name="wfsCsvUrl" select="concat($wfsUrl, '&amp;version=1.0.0&amp;request=GetFeature&amp;typeName=', $layerName, '&amp;outputFormat=csv')"/>
						<xsl:variable name="wfsShapeUrl" select="concat($wfsUrl, '&amp;version=1.0.0&amp;request=GetFeature&amp;typeName=', $layerName, '&amp;outputFormat=shape-zip')"/>

						<xsl:if test="count(//gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'OGC:WFS' and
                            normalize-space(gmd:CI_OnlineResource/gmd:linkage/gmd:URL) = $wfsUrl]) = 0">
							<gmd:onLine>
								<gmd:CI_OnlineResource>
									<gmd:linkage>
										<gmd:URL><xsl:value-of select="$wfsUrl"/></gmd:URL>
									</gmd:linkage>
									<gmd:protocol>
										<gco:CharacterString>OGC:WFS</gco:CharacterString>
									</gmd:protocol>
									<gmd:name><gco:CharacterString><xsl:value-of select="$layerName"/></gco:CharacterString></gmd:name>
									<gmd:description gco:nilReason="missing">
										<gco:CharacterString/>
									</gmd:description>
								</gmd:CI_OnlineResource>
							</gmd:onLine>
						</xsl:if>

						<xsl:if test="count(//gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'download' and
							normalize-space(gmd:CI_OnlineResource/gmd:linkage/gmd:URL) = $wfsCsvUrl]) = 0">
							<gmd:onLine>
								<gmd:CI_OnlineResource>
									<gmd:linkage>
										<gmd:URL><xsl:value-of select="$wfsCsvUrl"/></gmd:URL>
									</gmd:linkage>
									<gmd:protocol>
										<gco:CharacterString>download</gco:CharacterString>
									</gmd:protocol>
									<gmd:name>
										<gmx:MimeFileType type="text/csv">CSV</gmx:MimeFileType>
									</gmd:name>
									<gmd:description gco:nilReason="missing">
										<gco:CharacterString/>
									</gmd:description>
								</gmd:CI_OnlineResource>
							</gmd:onLine>
						</xsl:if>

						<xsl:if test="count(//gmd:onLine[normalize-space(gmd:CI_OnlineResource/gmd:protocol/gco:CharacterString) = 'download' and
							normalize-space(gmd:CI_OnlineResource/gmd:linkage/gmd:URL) = $wfsShapeUrl]) = 0">
							<gmd:onLine>
								<gmd:CI_OnlineResource>
									<gmd:linkage>
										<gmd:URL><xsl:value-of select="concat($wfsUrl, '&amp;version=1.0.0&amp;request=GetFeature&amp;typeName=', $layerName, '&amp;outputFormat=shape-zip')"/></gmd:URL>
									</gmd:linkage>
									<gmd:protocol>
										<gco:CharacterString>download</gco:CharacterString>
									</gmd:protocol>
									<gmd:name>
										<gmx:MimeFileType type="application/zip">Shape-zip</gmx:MimeFileType>
									</gmd:name>
									<gmd:description gco:nilReason="missing">
										<gco:CharacterString/>
									</gmd:description>
								</gmd:CI_OnlineResource>
							</gmd:onLine>
						</xsl:if>
					</xsl:for-each>

				</xsl:copy>
			</xsl:for-each>
		</xsl:copy>
	</xsl:template>

	<!-- ================================================================= -->
	<!-- copy everything else as is -->
	
	<xsl:template match="@*|node()">
	    <xsl:copy>
	        <xsl:apply-templates select="@*|node()"/>
      </xsl:copy>
	</xsl:template>

</xsl:stylesheet>
